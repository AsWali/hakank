/*
  Thermostat problem.

  OMEGA: Fast, casual Inference from Simple Parts 
  From  https://www.youtube.com/watch?v=oCvbqKE2tWA
  @ ~ 21min
  
  The Omega (Julia) model is here:
  https://github.com/zenna/Omega.jl/blob/master/OmegaModels/src/causal/thermostat.jl


  Cf ~/cplint/thermostat.pl

*/

type Time;
distinct Time morning, afternoon, evening;

random Time time ~ UniformChoice({t for Time t});

random Boolean is_window_open ~ BooleanDistrib(0.5);

// a.c. is off when window is closed
random Boolean is_ac_on ~
  if is_window_open then false
  else BooleanDistrib(0.5);

// hottest at noon, cool at night
random Real outside_temp ~
    if time == morning then Gaussian(20,2)
    else if time == afternoon then Gaussian(32,2)
    else if time == evening then Gaussian(10,2);
 

// a.c. chills the room
random Real room_temp ~
    if is_ac_on then Gaussian(20,2)
    else Gaussian(25,2);


// great insulation
random Real thermostat ~
    if is_window_open then (outside_temp + room_temp) / 2.0
    else room_temp;


// obs is_ac_on = true;
// obs is_window_open = false;
// obs room_temp < 20.0 = true;
// obs room_temp > 20.0 = true;
// obs time = evening;
// obs outside_temp > 10.0 = true; 

query time;
query is_ac_on;
query is_window_open;
query outside_temp;
query room_temp;
query thermostat;