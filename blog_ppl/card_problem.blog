/*
   From https://math.stackexchange.com/questions/513250/conditional-probability-question-with-cards-where-the-other-side-color-is-to-be
   """
   A box contains three cards. One card is red on both sides, one card is green on both sides, 
   and one card is red on one side and green on the other. One card is selected from the 
   box at random, and the color on one side is observed. If this side is green, what is 
   the probability that the other side of the card is also green?

   ... the answer to this question is 2/3.
   """

   Give that the color of card is green, the the probability of the different cards are
      Distribution of values for selected_card
         all_green       0.6680775028200873
         red_green       0.3319224971800305
      Distribution of values for card
         green   1.0
 
   I.e. the probability that it's the all green card is 2/3.

*/


type Color;
distinct Color red, green;

type Card;
distinct Card all_red, all_green, red_green;

// What card did we select?
random Card selected_card ~ Categorical({all_red->.333,all_green->0.3333,red_green->0.3333});

// What is the color of the card (one side) that we see?
random Color card ~
       case selected_card in
       {
         all_red ->  Categorical({red->1}),
         all_green ->  Categorical({green->1}),
         red_green ->  Categorical({red -> 0.5, green->0.5})
       };

// The color of the card is
obs card = green;

query selected_card;
query card;