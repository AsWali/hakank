/*
  http://cplint.eu/example/inference/viral.swinb
  """
  Viral Marketing

  A firm is interested in marketing a new product to its customers. These are connected in a social network 
  that is known to the firm: the network represents the trust relationships between customers. The firm has 
  decided to adopt a marketing strategy that involves giving the product for free to a number of its 
  customers, in the hope that these influence the other customers and entice them to buy the product. 
  The firm wants to choose the customers to which marketing is applied so that its return is maximized. 
  This involves computing the probability that the non-marketed customers will acquire the product given the 
  action to the marketed customers. 

  This viral marketing scenario is inspired by [1].

  We can model this domain with an LPAD where the predicate trust/2 encodes the links between customers in the 
  social network and predicate has/1 is true of customers that possess the product, either received as a gift 
  or bought. Predicate trust/2 is defined by a number of certain facts, while predicate has/1 is defined by 
  two rules, one expressing the prior probability of a customer to buy the product and one expressing the 
  fact that if a trusted customer has the product, then there is a certain probability that the trusting 
  customer buys the product. 
  """

  Reference G. V. den Broeck, I. Thon, M. van Otterlo, L. D. Raedt,
  Dtproblog: A decision-theoretic probabilistic prolog, in: M. Fox, D. Poole (Eds.),
  24th AAAI Conference on Artificial Intelligence, AAAI’10, Atlanta, Georgia, USA,
  July 11-15, 2010, AAAI Press, 2010, pp. 1217–1222.

  This BLOG model:
  Distribution of values for has(p2)
     false   0.6857457704025404
     true    0.31425422959745697


*/

type Person;
distinct Person p1,p2,p3,p4;

fixed Boolean trusts(Person a, Person b) =
      ListInterp(2, p2,p1,
                    p3,p1,
                    p3,p2,
                    p4,p1,
                    p4,p3);


random Boolean has(Person p) ~
   if exists Person q trusts(p, q) & has(q) then
      BooleanDistrib(0.4)
   else
      BooleanDistrib(0.1)
   ;


obs has(p3) = true;

query has(p1);
query has(p2);
query has(p3);
query has(p4);
query {p,has(p) for Person p};
query size({p for Person p: has(p) == true});

/*
  """
  We want to compute the probability that customer 2 buys the product if we perform the action of 
  giving the product to customer 3. We need to use causal reasoning so we use the action do(has(3)) 
  as evidence:
  ?- prob(has(2),do(has(3)),P).

  P = 0.136

  [Note that has(2) without do(has(3)) is also 0.136
    ?- prob(has(2),P).
    P = 0.136
  ]
  """
*/


/*
  """
  If instead we compute the effect of the action using regular probabilistic inference, we get:
  ?- prob(has(2),has(3),P).
  P = 0.4065135474609725
  So not distinguishing seeing from doing leads to an overly optimistic estimate. 
  ""
*/