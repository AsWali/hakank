/*
  Cookie problem.

  From Think Bayes, page 3
  """
  Suppose there are two bowls of cookies. 
  Bowl 1 contains 30 vanilla cookies and 10 chocolate cookies. 
  Bowl 2 contains 20 of each.

  Now suppose you choose one of the bowls at random and, without looking,
  select a cookie at random. The cookie is vanilla. 
  What is the probability that it came from Bowl 1?
  """

  Distribution of values for bowl
  bowl1   0.5956722099779828
  bowl2   0.4043277900220481


*/


type Cookie;
distinct Cookie vanilla, chocolate;


type Bowl;
distinct Bowl bowl1, bowl2;


random Bowl bowl ~ UniformChoice({b for Bowl b});

random Cookie cookie ~
   case bowl in
     {
        bowl1 -> Categorical({vanilla -> 30, chocolate -> 10 }),
        bowl2 -> Categorical({vanilla -> 20, chocolate -> 20 })
     };



obs cookie = vanilla;

query cookie;
query bowl;
