/*
  What is the color of the taxi?

  From https://www.bayesia.com/2018-03-02-probabilistic-reasoning-under-uncertainty

  Originally from Kahnemann, Slovic, Tversky "Judgement under uncertainty"
  

  There has been an accicent involving a taxi.
  There are two kinds of taxis:
    - yellow taxi: 85% 
    - white taxi: 15%

  A witness say: It was a white taxi involved in the accident.

  Researcher:
    - 80% of all witness statements are true
    - 20% of all witness statements are false.

  What is the probability that it was a white taxi involved in
  the accident?

  Answer: The probability that it was a white taxi involved in the accident
          is about 41%. And it's the same as in the talk.
  


*/

type Taxi;
distinct Taxi white, yellow;

// Prior distributions of the different taxis. 
random Taxi involved ~  Categorical({white->0.15, yellow -> 0.85});
// random Taxi involved ~  Categorical({white->0.5, yellow -> 0.5}); // experiment

// Witness says color but is is only x percent reliable.
// Witness experts states that a witness can only be 80% reliable
// (given the same circumstances as the accidents).
random Boolean witness(Taxi c) ~
       if c == involved then BooleanDistrib(0.80) else BooleanDistrib(0.2);
       /*
       // The Bayesian Network approach. Give the same result.
       case [involved,c] in
       {
          [white,white] -> BooleanDistrib(0.8),
          [white,yellow] -> BooleanDistrib(0.2),
          [yellow,yellow] -> BooleanDistrib(0.8),
          [yellow,white] -> BooleanDistrib(0.2)
       };
       */

/*
Distribution of values for involved
        yellow  0.5814751286449389
        white   0.4185248713550569  <-----
Distribution of values for witness(white)
        true    1.0
Distribution of values for witness(yellow)
        true    0.5467581475128678
        false   0.45324185248713156
======== Done ========
*/

obs witness(white) = true;
// obs witness(yellow) = true;

query involved;
query witness(white);
query witness(yellow);

