/*
   From Hugin's BN heart_disease_regression.net
   
   Risk of dying from heart disease in the next 10 years

   Darn, according to this model, there's a probability of 0.9999999983124701
   that I will die from a heart disease in the next 10 years...

*/

// type Sex; 
// distinct Sex male, female;

fixed Integer male = 0;
fixed Integer female = 1;

// Priors

// Risk of dying from heart disease in the next 10 years
random Real risk ~ 1.0/(1.0 + exp(-score));

random Integer sex ~ Categorical({male -> 0.5, female -> 0.5});

fixed Real intercept = -5.0;

random Real score ~ intercept + score_age + score_sex + score_cholesterol;

random Real score_cholesterol ~ 1.2 * (cholesterol - 5.0);

random Real score_sex ~ -1 * sex;

random Real score_age ~ 2.0 * (age - 50.0);

random Real cholesterol ~ Gaussian(6,10.0); // In the Hugin model the variance is 100.0!

random Real age ~ Gaussian(40,62.5); // In the Hugin model the variance is 625!

obs age = 62.0;
obs sex = male;
obs cholesterol = 5; // ???

// query sex;
// query age;
query cholesterol;
query score_cholesterol;
query score_sex;
query score_age;
query score;
query risk;
// query 1.0 - risk;


