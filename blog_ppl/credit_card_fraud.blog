/*
   Credit-card fraud.

   From David Heckerman
   "A Tutorial on Learning With Bayesian Networks"
   page 12ff
   """
   To illustrate the process of building a Bayesian network, consider the problem of de-
   tecting credit-card fraud. We begin by determining the variables to model. One possible
   choice of variables for our problem is Fraud(F), Gas(G), Jewelry(J) Age(A) and Sex(S) 
   representing whether or not the current purchase is fraudulent, whether or not there
   was a gas purchase in the last 24 hours, whether or not there was a jewelry purchase in
   the last 24 hours, and the age and sex of the card holder, respectively.
   """
   
*/

type Sex;
distinct Sex male, female;

type Age;
distinct Age age_below_30, age_30_to_50, age_above_50;

random Boolean fraud ~ BooleanDistrib(0.00001);
random Age age ~ Categorical({age_below_30 -> 0.25, age_30_to_50 -> 0.40, age_above_50-> 0.35});
random Sex sex ~ Categorical({male -> 0.5, female->0.5});

random Boolean gas ~ if fraud then BooleanDistrib(0.2) else BooleanDistrib(0.01);

random Boolean jewlery ~
       if fraud then BooleanDistrib(0.05)
       else
        case [age, sex] in
        {
          [age_below_30,male] -> BooleanDistrib(0.0001),
          [age_30_to_50,male] -> BooleanDistrib(0.0004),
          [age_above_50,male] -> BooleanDistrib(0.0002),
          [age_below_30,female] -> BooleanDistrib(0.0005),
          [age_30_to_50,female] -> BooleanDistrib(0.002),
          [age_above_50,female] -> BooleanDistrib(0.001)
        };


// obs age = age_below_30;
// obs age = age_30_to_50;
// obs age = age_above_50;
// obs gas = true;
// obs sex = female;
// obs jewlery = true;
obs fraud = true;

query fraud;
query age;
query sex;
query gas;
query jewlery;