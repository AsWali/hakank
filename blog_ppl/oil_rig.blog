/*
   From BayesiaLab, example Oil Rig

*/

type Oil;
distinct Oil dry, wet, soaking;

type ResultType;
distinct ResultType na, closed_structure, open_structure, no_structure;

random Boolean seismic_test ~ BooleanDistrib(0.5);

random Integer test_cost ~ if seismic_test then -10 else 0;

random Oil oil ~ Categorical({dry -> 50, wet -> 30, soaking->20});

random ResultType test_result ~
   if seismic_test then
      case oil in
        {
          dry -> Categorical({closed_structure->10,open_structure->30, no_structure->60}),
          wet -> Categorical({closed_structure->30,open_structure->40, no_structure->30}),
          soaking -> Categorical({closed_structure->50,open_structure->40, no_structure->10})
        }
   else
        na;


random Boolean drill ~ BooleanDistrib(0.5);

random Integer gain ~
    if drill then
       case oil in
       {
          dry -> -70000,
          wet -> 50000,
          soaking -> 200000
       }
    else 0;

// obs drill = false;
// obs seismic_test = true;
// obs oil = soaking;
// obs test_result = open_structure;
// obs drill = true;


query seismic_test;
query test_cost;
query oil;
query test_result;
query drill;
query gain;
