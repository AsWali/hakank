/*

  https://edu.swi-prolog.org/mod/assign/view.php?id=249
  """
  Medical diagnosis

  Develop an expert system for medical diagnosis.

  Consider three diseases: flu, gastroenteritis and bronchitis.

  A priori, flu has probability 0.3, gastroenteritis 0 0.2, and bronchitis 0.25.

  If you have the flu, you may have the following symptons, associated with their
  probabilities (symptoms are not mutually exclusive):

    fever, 0.8
    cough 0.6
    sore throat 0.5
    headaches 0.4
    aches 0.7

  If you have gastroenteritis, you may have the following symptons, associated with their
  probabilities (symptoms are not mutually exclusive):

    diarrhea 0.8
    aches 0.7
    nausea 0.4
    fatigue 0.3

  If you have bronchitis, you may have the following symptons, associated with their probabilities
  (symptoms are not mutually exclusive):

    cough 0.8
    fatigue 0.7
    fever 0.3

  Compute the probability of each disease given that the patient has the symptoms fever and aches.

  Do the same supposing the patient adds that he also experiences fatigue.
  """

  Cf ~/cplint/course_medical_diagnosis.pl

*/

/*
  Consider three diseases: flu, gastroenteritis and bronchitis.

  A priori, flu has probability 0.3, gastroenteritis 0.2, and bronchitis 0.25.

*/

/*
type Disease;
distinct Disease flu, gastroenteritis, bronchitis;

type Symptom;
distinct Symptom fever, cough, sore_throat, headaches, aches, diarrhea, nausea, fatigue;
*/

random Boolean flu ~ BooleanDistrib(0.3);
random Boolean gastroenteritis ~ BooleanDistrib(0.2);
random Boolean bronchitis ~ BooleanDistrib(0.25);

/*
  If you have the flu, you may have the following symptons, associated with their
  probabilities (symptoms are not mutually exclusive):

    fever, 0.8
    cough 0.6
    sore throat 0.5
    headaches 0.4
    aches 0.7
*/

random Boolean fever ~ 
      if flu then BooleanDistrib(0.8) 
      else if bronchitis then BooleanDistrib(0.3);

random Boolean cough ~  
      if flu then BooleanDistrib(0.6)
      else if bronchitis then BooleanDistrib(0.8);

random Boolean sore_throat ~ 
      if flu then BooleanDistrib(0.5);

random Boolean headaches ~ 
      if flu then BooleanDistrib(0.4);

random Boolean aches ~ 
      if flu then BooleanDistrib(0.7)
      else if gastroenteritis then BooleanDistrib(0.7);



/*
  If you have gastroenteritis, you may have the following symptons, associated with their
  probabilities (symptoms are not mutually exclusive):

    diarrhea 0.8
    aches 0.7
    nausea 0.4
    fatigue 0.3
*/

random Boolean diarrhea ~ 
     if gastroenteritis then BooleanDistrib(0.8);

// random Boolean aches ~ Categorical({gastroenteritis -> BooleanDistrib(0.7)});
random Boolean nausea ~ 
     if gastroenteritis then BooleanDistrib(0.4);

random Boolean fatigue ~ 
     if gastroenteritis then BooleanDistrib(0.3)
     else if bronchitis then BooleanDistrib(0.7);

/*
  If you have bronchitis, you may have the following symptons, associated with their probabilities
  (symptoms are not mutually exclusive):

    cough 0.8
    fatigue 0.7
    fever 0.3
*/

// random Boolean cough ~ Categorical({bronchitis -> BooleanDistrib(0.8)});
// random Boolean fatigue ~ Categorical({bronchitis -> BooleanDistrib(0.7)});
// random Boolean fever ~ Categorical({bronchitis -> BooleanDistrib(0.3)});


obs fever = true;
obs aches = true;

query flu;
query gastroenteritis;
query bronchitis;
