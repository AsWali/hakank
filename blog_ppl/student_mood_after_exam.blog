/*
  From Joost-Pieter Katoen
  "Probabilistic Programming Quantitative Modeling for the Masses?"
  (MMB 2018 Conference, Erlangen)
  
  Slide 4:
  """
  How likely does a student end up with a bad mood after getting
  a bad grade for an easy exam, given that she is well prepared?
  """
*/

Type Difficulty;
distinct Difficulty easy, hard;

Type GoodBad;
distinct GoodBad good, bad;


random Difficulty difficulty ~ Categorical({easy->0.6, hard->0.4});

random GoodBad preparation ~ Categorical({bad->0.7, good->0.3});

random GoodBad grade ~
       case [difficulty, preparation] in
       {
         [easy, bad] -> Categorical({bad->0.95, good->0.05}),
         [easy, good] -> Categorical({bad->0.5, good->0.5}),
         [hard, bad] -> Categorical({bad->0.6, good->0.4}),
         [hard, good] -> Categorical({bad->0.05, good->0.95})
       };

random GoodBad mood ~
       if grade == bad then Categorical({bad->0.9, good->0.1})
       else Categorical({bad->0.3, good->0.7});


obs difficulty = easy;
obs preparation = good;
obs grade = bad;

query difficulty;
query preparation;
query grade;
query mood;