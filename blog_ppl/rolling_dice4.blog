/*
  https://dtai.cs.kuleuven.be/problog/tutorial/basic/03_dice.html
  """
  Letâ€™s consider an infinite number of dice, which we roll one after the other until we see a 
  six for the first time. What is the probability of stopping after n dice? The first die is always rolled, 
  those with higher numbers D are only rolled if the previous roll did not stop the process.
  """

  Cf dice_6_throws.blog
  
*/


/*
1/6::dice(1,1); 1/6::dice(2,1); 1/6::dice(3,1); 1/6::dice(4,1); 1/6::dice(5,1); 1/6::dice(6,1).

1/6::dice(1,D); 1/6::dice(2,D); 1/6::dice(3,D); 1/6::dice(4,D); 1/6::dice(5,D); 1/6::dice(6,D) :- D > 1, P is D-1, continue(P).

stop(N) :- dice(6,N).
continue(N) :- dice(X,N), X < 6.

query(stop(N)) :- between(1,5,N).

% closed form probability computation
%query(s(X)) :- between(1,5,X).
%P::s(N) :- P is 1/6*(5/6)**(N-1).
*/


// Did we stop at time t?
random Boolean stop(Integer tt) ~
    if dice(tt) == 6 then true else false;

// Note: we start at time 1 here
random Integer dice(Integer t) ~
    // Always throw first time
    if t == 1 then UniformInt(1,6)
    else
      // We throw until the last time was a 6.
      // Note that the null condition is needed here...
      if dice(t-1) != null & !stop(t-1) then UniformInt(1,6);

// We start at time 1 here as well.
fixed Real closed_form(Integer n) = (1/6.0) * ((5/6.0)^(n-1));

query stop(1);
query stop(2);
query stop(3);
query stop(4);
query stop(5);
query stop(6);

query closed_form(1);
query closed_form(2);
query closed_form(3);
query closed_form(4);
query closed_form(5);
query closed_form(6);