/*
  Where is my bag problem.

  From
  "Probabilistic Reasoning Under Uncertainty with Bayesian Networks"
  https://www.bayesia.com/2018-03-02-probabilistic-reasoning-under-uncertainty

  (Also see The BaysiaLabBook "Bayesian Networks BayesiaLab" v1, page 51f.)

  Travel from Singapore -> Tokyo -> Los Angeles.
  1) The departure of Singapore -> Tokyo is delayed
  2) Just made the Tokyo -> LA trip. There's a 50/50 change the luggage is on the plane to LA.
  3) Arriving at LA, waiting for the luggage in 5 minutes, the luggage has not been seen.
  What is the probability that the bag was in the Tokyo -> LA plane?
  
  Answer: The probability that the bag was with the LA plane is 33% (as in the talk).

*/

fixed Integer maxTime = 10;

// We know that the bag was on the plane with 50% probability.
// Variant: https://www.youtube.com/watch?v=c71pnonOMkI&t=1074s it's 80/20.
random Boolean bag_on_plane ~ BooleanDistrib(0.5);
// random Boolean bag_on_plane ~ BooleanDistrib(0.8);

// We assume uniform time step
random Integer time ~ UniformInt(0,maxTime);

// Probability that the bag is on the carousel given a specific time
// (if it was on the plane, that is).
// The probability that the bag is on the carousel on a given time
// is directly proportional on the time:
//     time / maxTime
random Boolean bag_on_carousel ~
      if bag_on_plane then BooleanDistrib(1.0*time/maxTime);

// He waited 5 minutes without seeing the luggage.
obs time = 5;
obs bag_on_carousel = false;

query time;
query bag_on_carousel;
query bag_on_plane;
