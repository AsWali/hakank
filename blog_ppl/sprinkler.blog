/*
  http://www.cs.ubc.ca/~murphyk/Bayes/bnintro.html

  Cf ~/problog/sprinkler.pl
*/

random Boolean cloudy ~ BooleanDistrib(0.5);

random Boolean sprinkler ~
  if cloudy then BooleanDistrib(0.1) else BooleanDistrib(0.5);


random Boolean rain ~
   if cloudy then BooleanDistrib(0.8) else BooleanDistrib(0.2);


random Boolean wet_grass ~
   case [sprinkler, rain] in
     {
        [false,false] -> false,
        [true,false] -> BooleanDistrib(0.9),
        [false,true] -> BooleanDistrib(0.9),
        [true,true] -> BooleanDistrib(0.99)
     };

/*
Distribution of values for cloudy
        true    0.579283582925904
        false   0.42071641707409124
Distribution of values for sprinkler
        false   0.5841727021839986
        true    0.41582729781600275
Distribution of values for rain
        true    0.7140775814970161
        false   0.28592241850300515
Distribution of values for wet_grass
        true    1.0
*/
obs wet_grass = true;
// obs wet_grass = false;

query cloudy;
query sprinkler;
query rain;
query wet_grass;
