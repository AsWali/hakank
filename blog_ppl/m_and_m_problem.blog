/*
  M & M problem.

  From Think Bayes, page 6f.
  """
  M&M’s are small candy-coated chocolates that come in a variety of colors.
  Mars, Inc., which makes M&M’s, changes the mixture of colors from time
  to time.

  In 1995, they introduced blue M&M’s. Before then, the color mix in a bag
  of plain M&M’s was 30% Brown, 20% Yellow, 20% Red, 10% Green, 10%
  Orange, 10% Tan. Afterward it was 24% Blue , 20% Green, 16% Orange,
  14% Yellow, 13% Red, 13% Brown.

  Suppose a friend of mine has two bags of M&M’s, and he tells me that one
  is from 1994 and one from 1996. He won’t tell me which is which, but he
  gives me one M&M from each bag. One is yellow and one is green. What is
  the probability that the yellow one came from the 1994 bag?

  """

  Page 7 (the table): The answer is 20/27: ~0.74074.


  Output:
  Distribution of values for mix(0)
        mix1994 0.7403564679290108
        mix1996 0.25964353207099533
  Distribution of values for mix(1)
        mix1996 0.7403564679290108
        mix1994 0.25964353207099533
  Distribution of values for color(0)
        yellow  1.0
  Distribution of values for color(1)
        green   1.0



*/


type Color;
distinct Color brown, yellow, red, green, orange, tan, blue;

type Mix;
distinct Mix mix1994, mix1996;

// First pick a bag in mix(0) and then pick the other bag in mix(1)
random Mix mix(Integer i) ~
       if i == 0 then UniformChoice({m for Mix m})
       else
       if mix(0) == mix1994 then mix1996 else mix1994;

random Color color(Integer i) ~
  case mix(i) in
   {
     mix1994 -> Categorical({brown->30, yellow -> 20, red -> 20, green->10, orange->10, tan->10}),
     mix1996 -> Categorical({blue->24, green->20, orange->16, yellow->14, red->13, brown->13})
   };

obs color(0) = yellow;
obs color(1) = green;

query mix(0);
query mix(1);
query color(0);
query color(1);

