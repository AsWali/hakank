/*

   From the AgenaRisk model Tutorial/Hypothesis Testing
   Comparison of two materials A and B which has different number of tests of faultyness:

   - A was tested in 200 cases where 10 was faulty
   - B was tested in 100 cases where 9 was fault.

   Is A better then B?

   (Note: This is a variant of A/B tests.)
   

*/

fixed Integer a_tests = 200;
fixed Integer b_tests = 100;

random Real prob_a_is_faulty ~ UniformReal(0,1);
random Real prob_b_is_faulty ~ UniformReal(0,1);

random Integer a_faults ~ Binomial(a_tests,prob_a_is_faulty);
random Integer b_faults ~ Binomial(b_tests,prob_b_is_faulty);

type Hypothesis;
distinct Hypothesis A_better_than_B, A_not_better_than_B;

random Hypothesis hypothesis ~ if prob_a_is_faulty < prob_b_is_faulty then A_better_than_B else A_not_better_than_B;

obs a_faults = 10;
obs b_faults = 9;

query prob_a_is_faulty;
query prob_b_is_faulty;

query prob_a_is_faulty - prob_b_is_faulty;
query hypothesis;