/*

  Firing squad example (from Judea Pearl)


  court_order: a court order to shoot a prisoner is given
  captain_signals: captain signal to a and b to shoot the prisoner
  a_shoots: person a shoots at the prisoner
  b_shoots: person b shoots at the prisoner
  death: the prisoner is dead (due to the shooting of a and/or b)

  Cf the ProbLog model firing_squad.pl

  See ~/cpling/firing_squad.pl

*/

// Note: BooleanDistrib generates true|false (Bernoulli generates 0|1)
random Boolean court_order ~ BooleanDistrib(0.8);

random Boolean captain_signals ~
    if court_order then BooleanDistrib(0.9) else BooleanDistrib(0.1);

random Boolean a_shoots ~
    if captain_signals then BooleanDistrib(0.95) else BooleanDistrib(0.1);

random Boolean b_shoots ~
    if captain_signals then BooleanDistrib(0.95) else BooleanDistrib(0.1);

random Boolean death ~
  if a_shoots | b_shoots then BooleanDistrib(0.9);

// obs death = true;
obs captain_signals = false;
// obs b_shoots = false;

query court_order;
query captain_signals;
query a_shoots;
query b_shoots;
query death;