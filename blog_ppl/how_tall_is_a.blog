/*
  https://www.allendowney.com/blog/2018/10/18/how-tall-is-a/
  """
  Here are a series of problems I posed in my Bayesian statistics class:

  1) Suppose you meet an adult resident of the U.S. who is 170 cm tall. 
     What is the probability that they are male?

  2) Suppose I choose two U.S. residents at random and A is taller than B.  
     How tall is A?

  3) In a room of 10 randomly chosen U.S. residents, A is the second tallest.  
     How tall is A?  
     And what is the probability that A is male?

  As background: For adult male residents of the US, the mean and standard deviation of 
  height are 178 cm and 7.7 cm. For adult female residents the corresponding stats 
  are 163 cm and 7.3 cm.  And 51% of the adult population is female.

  """
*/


type Gender;
distinct Gender male, female;

type Person;
distinct Person p[10];

random Real height(Person p) ~ if gender(p) == male then Gaussian(178,7.7) else Gaussian(163,7.3);
random Gender gender(Person p) ~ Categorical({male -> 0.49, female -> 0.51});

/*
  1) Suppose you meet an adult resident of the U.S. who is 170 cm tall. 
     What is the probability that they are male?

  Distribution of values for gender(p[0])
        female  0.7063171578494144
        male    0.29368284215056817
*/

/*
obs height(p[0]) = 170;
query gender(p[0]);
*/

/*
  2) Suppose I choose two U.S. residents at random and A is taller than B.  
     How tall is A?

    Mean: 174.914897211492 cm
 
*/

/*
obs height(p[0]) > height(p[1]) = true;
query height(p[0]);
*/


/*
  3) In a room of 10 randomly chosen U.S. residents, A is the second tallest.  
     How tall is A?  
     And what is the probability that A is male?


     How tall is A: Mean: 178.77824900061

     Distribution of values for gender(p[1])
        male    0.9827586206896555
        female  0.01724137931034488


*/

obs height(p[0]) > height(p[1]) = true; // A (p[1]) is second tallest
obs height(p[1]) > height(p[2]) = true; // p[1] is taller than anyone else
obs height(p[1]) > height(p[3]) = true;
obs height(p[1]) > height(p[4]) = true;
obs height(p[1]) > height(p[5]) = true;
obs height(p[1]) > height(p[6]) = true;
obs height(p[1]) > height(p[7]) = true;
obs height(p[1]) > height(p[8]) = true;
obs height(p[1]) > height(p[9]) = true;

query height(p[1]);
query gender(p[1]);
