/*
   From https://reference.wolfram.com/language/ref/HypergeometricDistribution.html
   """
   Suppose an urn has 100 elements, of which 40 are special.
   ...
   Compute the probability that there are more than 25 special elements in a 
   draw of 50 elements.
     Answer: 0.0120902

   Compute the expected number of special elements in a draw of 50 elements.
     Answer: 20
   """

   Approach:
     - generate all the elements randomly. And then check how many of the first 50 are 25

*/

type Element;
distinct Element special, nonspecial;

fixed Integer N = 100;
fixed Integer NumSpecial = 40;

random Element element(Integer i) ~ Categorical({nonspecial -> N-NumSpecial, special -> NumSpecial});

// We have exactly 40 special elements (no random there!)
obs sum({1 for Integer i: i >= 1 & i <= N & element(i) == special}) = 40.0;


// What's the probability that there are more than 25 special elements in a draw of 50 elements
// This model give p(0.011693315116378764) for 1000000 samples (0.07942 possible worlds)
query sum({1 for Integer i: i >= 1 & i <= 50 & element(i) == special}) > 25.0;

// Compute the expected number of special elements in a draw of 50 elements.
query sum({1 for Integer i: i >= 1 & i <= 50 & element(i) == special});


// query sum({1 for Integer i: i >= 1 & i <= N & element(i) == special}); // Number of specials
// query sum({1 for Integer i: i >= 1 & i <= N & element(i) == nonspecial}); // Number of non specials
