/*
   From Eugene Charniak
   "Bayesian Networks without Tears"
   page 51
   
   """
   Suppose when I go home at night, I want to know if my family is home before I try the doors. 
   (Perhaps the most convenient door to enter is double locked when nobody is home.) 
   Now, often when my wife leaves the house, she turns on an outdoor light. However, 
   she sometimes turns on this light if she is expecting a guest. Also, we have a dog. 
   When nobody is home, the dog is put in the back yard. The same is true if the dog
   has bowel troubles. Finally, if the dog is in the backyard, I will probably hear her barking (or
   what I think is her barking), but sometimes I can be confused by other dogs barking. This
   example, partially inspired by Pearl’s (1988) earthquake example, is illustrated in figure 1.
   There we find a graph not unlike many we see in AI. We might want to use such diagrams to
   predict what will happen (if my family goes out, the dog goes out) or to infer causes from
   observed effects (if the light is on and the dog is out, then my family is probably out).

   The important thing to note about this example is that the causal connections are
   not absolute. Often, my family will have left without putting out the dog or turning on a
   light. Sometimes we can use these diagrams anyway, but in such cases, it is hard to know
   what to infer when not all the evidence points the same way. Should I assume the family is
   out if the light is on, but I do not hear the dog? What if I hear the dog, but the light is
   out? Naturally, if we knew the relevant probabilities, such as P(family-out | light-on, ¬ hear-
   bark), then we would be all set. However, typically, such numbers are not available for
   all possible combinations of circumstances. Bayesian networks allow us to calculate them
   from a small set of probabilities, relating only neighboring nodes.
   """

   cf ~/blog/family_out_problem.blog
   
*/

// E[family_out,bowel_problem,light_on,dog_out,hear_bark] = (4420548/8831351,55001/8831351,1,3764630/8831351,0)
// (0.500551727589584,0.00622792594247471,1,0.426280192011392,0)

def main() {

  family_out := flip(0.15);
  bowel_problem := flip(0.01);
  light_on := 0;
  if family_out {
    light_on = flip(0.6);
  } else {
    light_on = flip(0.05);  
  }

  dog_out := 0;
  if family_out && bowel_problem {
    dog_out = flip(0.99);
  } else if family_out && ! bowel_problem {
    dog_out = flip(0.90);
  } else if !dog_out && bowel_problem {
    dog_out = flip(0.97);
  } else {
    dog_out = flip(0.3);
  }

  hear_bark := 0;
  if dog_out {
    hear_bark = flip(0.7);
  } else {
    hear_bark = flip(0.01);
  } 

  /*
  To take the earlier example, if I observe that the light is on (light-on = true) but do not hear my dog
  (hear-bark = false), I can calculate the conditional probability of family-out given these
  pieces of evidence. (For this case, it is .5.)
  */
  observe(light_on = true);
  observe(hear_bark = false);

  return(
         family_out,
         bowel_problem,
         light_on,
         dog_out,
         hear_bark
       );
}

