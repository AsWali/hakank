/*
  M & M problem.

  From Think Bayes, page 6f.
  """
  M&M’s are small candy-coated chocolates that come in a variety of colors.
  Mars, Inc., which makes M&M’s, changes the mixture of colors from time
  to time.

  In 1995, they introduced blue M&M’s. Before then, the color mix in a bag
  of plain M&M’s was 30% Brown, 20% Yellow, 20% Red, 10% Green, 10%
  Orange, 10% Tan. Afterward it was 24% Blue , 20% Green, 16% Orange,
  14% Yellow, 13% Red, 13% Brown.

  Suppose a friend of mine has two bags of M&M’s, and he tells me that one
  is from 1994 and one from 1996. He won’t tell me which is which, but he
  gives me one M&M from each bag. One is yellow and one is green. What is
  the probability that the yellow one came from the 1994 bag?

  """

  Page 7 (the table): The answer is 20/27: ~0.74074.

  cf ~/blog/m_and_m_problem.blog

  Output:
  Distribution of values for mix(0)
        mix1994 0.7403564679290108
        mix1996 0.25964353207099533
  Distribution of values for mix(1)
        mix1996 0.7403564679290108
        mix1994 0.25964353207099533
  Distribution of values for color(0)
        yellow  1.0
  Distribution of values for color(1)
        green   1.0


*/

// E[r₁,r₂,r₃,r₄] = (20/27,7/27,7/27,20/27)
// (0.740740740740741,0.259259259259259,0.259259259259259,0.740740740740741)
def main() {
  // colors
  brown := 0;
  yellow := 1;
  red := 2;
  green := 3;
  orange := 4;
  tan := 5;
  blue := 6;
  colors := [brown,yellow,red,green,orange,tan,blue];

  // mixes
  mix1994 := 0;
  mix1996 := 1;
  mixes := [mix1994,mix1996];

  n := 2;
  // First pick a bag in mix(0) and then pick the other bag in mix(1)
  mix := array(n);
  for i in [0..n) {
    if i = 0 {
      mix[i] = mixes[categorical([1/2,1/2])];
    } else {
      if mix[0] = mix1994  {
        mix[i] = mix1996;
      } else {
        mix[i] = mix1994;
      }
      
    }  
  }

  color := array(n);
  for i in [0..n) {
    if mix[i] = mix1994 {
      color[i] =categorical([30/100,20/100,20/100,10/100,10/100,10/100,0/100]);
    } else {
     color[i] = categorical([13/100,14/100,13/100,20/100,16/100,0/100,24/100]);
   }
  }

  observe(color[0] = yellow);
  observe(color[1] = green);  

  return(mix[0] = mix1994,
         mix[0] = mix1996,
         mix[1] = mix1994,
         mix[1] = mix1996
         );

}
