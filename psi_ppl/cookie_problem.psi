/*
  Cookie problem.

  From Think Bayes, page 3
  """
  Suppose there are two bowls of cookies. 
  Bowl 1 contains 30 vanilla cookies and 10 chocolate cookies. 
  Bowl 2 contains 20 of each.

  Now suppose you choose one of the bowls at random and, without looking,
  select a cookie at random. The cookie is vanilla. 
  What is the probability that it came from Bowl 1?
  """

  cf ~/blog/cookie_problem.blog
  
  Distribution of values for bowl
  bowl1   0.5956722099779828
  bowl2   0.4043277900220481


*/

// E[r₁,r₂,r₃,r₄] = (1,0,3/5,2/5)
// (1,0,0.6,0.4)
def main() {

  vanilla := 0;
  chocolate := 1;
  cookieType := [vanilla,chocolate];

  bowl1 := 0;
  bowl2 := 1;
  bowls := [bowl1,bowl2];

  bowl := bowls[categorical([1/2,1/2])];

  cookie := 0;
  if bowl = bowl1 {
    cookie = cookieType[categorical([30/40, 10/40])];
  } else {
    cookie = cookieType[categorical([20/40, 20/40])];
  }

  observe(cookie = vanilla);

  return(cookie = vanilla,
         cookie = chocolate,
         bowl = bowl1,
         bowl = bowl2,
         );

}