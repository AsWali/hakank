/*
   From the Netica model Book Bags.neta
   """
   Book Bags                  Copyright 1998 Norsys Software Corp.

   There are two book bags each containing 10 poker chips.  In one
   bag there are 7 red and 3 blue.  In the other bag there are 3 
   red and 7 blue. Five chips are drawn out of one of the bags and 
   shown to the subject (one at a time then returned to the bag).  
   The subject does not know which bag the chips came from.  
   There is an equal chance that the draws are made from either
   bag. After each draw the subject reports which bag he believes
   the chips are coming from and provides a probability that the 
   chips are being drawn from that bag.
      
   The problem comes from the early "revision of judgment" work 
   that indicated that people were conservative with respect to Bayes.   
   """

   cf ~/blog/book_bags.blog
   
*/

// observe draw[0] and draw[1] are both blue
// E[r₁,r₂,r₃,r₄,r₅,r₆,r₇,r₈,r₉,r₁₀,r₁₁,r₁₂] = (9/58,49/58,0,1,0,1,21/58,37/58,21/58,37/58,21/58,37/58)
// (0.155172413793103,0.844827586206897,0,1,0,1,0.362068965517241,0.637931034482759,0.362068965517241,0.637931034482759,0.362068965517241,0.637931034482759)

// observe all are 5 chips are blue
// E[r₁,r₂,r₃,r₄,r₅,r₆,r₇,r₈,r₉,r₁₀,r₁₁,r₁₂] = (243/17050,16807/17050,0,1,0,1,0,1,0,1,0,1)
// (0.0142521994134897,0.98574780058651,0,1,0,1,0,1,0,1,0,1)
def main() {
  // bags
  bag1 := 0;
  bag2 := 1;

  // colors
  red := 0;
  blue := 1;
  colors := [red,blue];

  bag := [bag1,bag2][categorical([0.5, 0.5])];

  n := 5;
  draw := array(n);
  for i in [0..n) {
    if bag == bag1 {
      draw[i] = colors[categorical([0.7, 0.3])];
    } else {
      draw[i] = colors[categorical([0.3, 0.7])];
    }
  }

/*
  observe(draw[0] = red);
  observe(draw[1] = red);
  observe(draw[2] = red);
  observe(draw[3] = red);
  observe(draw[4] = red);
*/

  observe(draw[0] = blue);
  observe(draw[1] = blue);
  observe(draw[2] = blue);
  observe(draw[3] = blue);
  observe(draw[4] = blue);



  return(bag=bag1,
         bag=bag2,
         draw[0]=red, 
         draw[0]=blue,        
         draw[1]=red,
         draw[1]=blue,         
         draw[2]=red,
         draw[2]=blue,        
         draw[3]=red,
         draw[3]=blue,         
         draw[4]=red,
         draw[4]=blue         
       );
}