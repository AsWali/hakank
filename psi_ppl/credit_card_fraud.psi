/*
   Credit-card fraud.

   From David Heckerman
   "A Tutorial on Learning With Bayesian Networks"
   page 12ff
   """
   To illustrate the process of building a Bayesian network, consider the problem of de-
   tecting credit-card fraud. We begin by determining the variables to model. One possible
   choice of variables for our problem is Fraud(F), Gas(G), Jewelry(J) Age(A) and Sex(S) 
   representing whether or not the current purchase is fraudulent, whether or not there
   was a gas purchase in the last 24 hours, whether or not there was a jewelry purchase in
   the last 24 hours, and the age and sex of the card holder, respectively.
   """
   
  cf ~/blog/credit_card_fraud.blog
  BLOG model:
Distribution of values for fraud
	true	1.0
Distribution of values for age
	age_30_to_50	0.3989999999999987
	age_above_50	0.3518699999999978
	age_below_30	0.24913000000000046
Distribution of values for sex
	male	0.5002300000000008
	female	0.4997700000000009
Distribution of values for gas
	false	0.8007399999999988
	true	0.19926000000000288
Distribution of values for jewlery
	false	0.9513800000000012
	true	0.048620000000000434

*/

// E[fraud_,r1_,r2_,r3_,r4_,r5_,gas_,jewlery_] = (1,1/4,2/5,7/20,1/2,1/2,1/5,1/20)
// (1,0.25,0.4,0.35,0.5,0.5,0.2,0.05)
def main() {

  // sex
  male := 0;
  female := 1;
  sexes := [male,female];

  // age groups
  age_below_30 := 0;
  age_30_to_50 := 1;
  age_above_50 := 2;
  ages := [age_below_30,age_30_to_50,age_above_50];
  
  fraud := flip(0.00001);
  
  age := ages[categorical([25/100, 40/100, 35/100])];
  sex := sexes[categorical([0.5,0.5])];

  gas := 0;
  if fraud {
    gas = flip(0.2); 
  } else {
    gas = flip(0.01);
  } 

  jewlery := 0;
  if fraud {
    jewlery = flip(0.05);
  } else {
    if        age = age_below_30 && sex = male {
      jewlery = flip(0.0001);
      
    } else if age = age_30_to_50 && sex = male {
      jewlery = flip(0.0004);
      
    } else if age = age_above_50 && sex = male {
      jewlery = flip(0.0002);
      
    } else if age = age_below_30 && sex = female {
      jewlery = flip(0.0005);
      
    } else if age = age_30_to_50 && sex = female {
      jewlery = flip(0.002);
      
    } else { // if age = age_above_50 && sex = female {
      jewlery = flip(0.001);
    }
  }

  // observe(age = age_below_30);
  // observe(age = age_30_to_50);
  // observe(age = age_above_50);
  // observe(gas = true);
  // observe(sex = female);
  // observe(jewlery = true);

  observe(fraud = true);

  return(fraud,
         age = age_below_30,
         age = age_30_to_50,
         age = age_above_50,         
         sex = male,
         sex = female,         
         gas,
         jewlery
         );
         
}
