/*

  What is the color of the taxi?

  From https://www.bayesia.com/2018-03-02-probabilistic-reasoning-under-uncertainty

  Originally from Kahnemann, Slovic, Tversky "Judgement under uncertainty"
  

  There has been an accicent involving a taxi.
  There are two kinds of taxis:
    - yellow taxi: 85% 
    - white taxi: 15%

  A witness say: It was a white taxi involved in the accident.

  Researcher:
    - 80% of all witness statements are true
    - 20% of all witness statements are false.

  What is the probability that it was a white taxi involved in
  the accident?

  Answer: The probability that it was a white taxi involved in the accident
          is about 41%. And it's the same as in the talk.


  E[white_involved_] = 12/29
  ~0.41379310344827586207


  See ~/blog/color_of_the_taxi.blog

*/

// Witness says a color but is only X percent reliable.
// Witness experts states that a witness can only be 80% reliable
// (given the same circumstances as the accidents).
def witness(c,involved) {
  if c = involved {
    return flip(0.8);
  } else {
    return flip(0.2);
  }
}

def main() {
  // the taxis
  white  := 0;
  yellow := 1;
  taxis := [white,yellow];

  // Prior distributions of the different taxis. 
  involved := taxis[categorical([0.15,0.85])];

  // Witness says the taxi was white.
  observe(witness(white,involved));

  // What is the probability that a white taxi
  // was in fact involved?
  return(involved = white);

}

