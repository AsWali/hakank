/*

  https://www.allendowney.com/blog/2018/10/18/how-tall-is-a/
  """
  Here are a series of problems I posed in my Bayesian statistics class:

  1) Suppose you meet an adult resident of the U.S. who is 170 cm tall. 
     What is the probability that they are male?

  2) Suppose I choose two U.S. residents at random and A is taller than B.  
     How tall is A?

  3) In a room of 10 randomly chosen U.S. residents, A is the second tallest.  
     How tall is A?  
     And what is the probability that A is male?

  As background: For adult male residents of the US, the mean and standard deviation of 
  height are 178 cm and 7.7 cm. For adult female residents the corresponding stats 
  are 163 cm and 7.3 cm.  And 51% of the adult population is female.

  """


*/

/*
  1) Suppose you meet an adult resident of the U.S. who is 170 cm tall. 
     What is the probability that they are male?

   gender[0] = male:
   E[r_] = (5/77)^(1/2)*1/((5/73)^(1/2)*1/E^(245/73)*1/Pi^(1/2)*51/100+(5/77)^(1/2)*1/E^(320/77)*1/Pi^(1/2)*49/100)*1/E^(320/77)*1/Pi^(1/2)*49/100
   (0.296013)

*/
def test1() {
   male := 0;
   female := 1;

   n := 1;

   height := array(n);
   gender := array(n);
   for p in [0..n) {
     gender[p] = [male,female][categorical([0.49,0.51])];
     
     if gender[p] = male {
       height[p] = gauss(178,7.7);
     } else {
       height[p] = gauss(163,7.3);
     }     
   } 

   cobserve(height[0],170);
   return gender[0] == male;


}


/*
  2) Suppose I choose two U.S. residents at random and A is taller than B.  
     How tall is A?

    --expectation returns very large expressions that Mathematica takes long time to solve.
    Maple evaluates (feval) to 0!
 
*/
def test2() {
   male := 0;
   female := 1;

   n := 2;

   height := array(n);
   gender := array(n);
   for p in [0..n) {
     gender[p] = [male,female][categorical([0.49,0.51])];
     
     if gender[p] = male {
       height[p] = gauss(178,7.7);
     } else {
       height[p] = gauss(163,7.3);
     }     
   } 
  
   observe(height[0] > height[1]);

   return(height[0],
          // height[1],
         )

}

/*
  3) In a room of 10 randomly chosen U.S. residents, A is the second tallest.  
     How tall is A?  
     And what is the probability that A is male?
 
  --expectation returns very large expressions...

*/
def test3() {
   male := 0;
   female := 1;

   // n := 10;
   n := 3; // let's do this for 3 people instead

   height := array(n);
   gender := array(n);
   for p in [0..n) {
     gender[p] = [male,female][categorical([0.49,0.51])];
     
     if gender[p] = male {
       height[p] = gauss(178,7.7);
     } else {
       height[p] = gauss(163,7.3);
     }
   }
   for p in [1..n) {
     observe(height[p-1] > height[p]);  
   }


   return(  height[1],
            gender[1]
         );

}

def main() {
  // return test1();
  // return test2();
  return test3();    
}