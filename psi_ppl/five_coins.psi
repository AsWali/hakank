/*

  From https://math.stackexchange.com/questions/3633307/bayes-rule-broken
  """
  Bayes' Rule broken?!?!

  This question has been driving me CRAZY for 4 days now. The question comes from 
  the textbook 'One Thousand Exercises in Probability', specifically Exercise 3 in 
  section 1.4. The solution does not make sense! The question goes as follows:

     'A man possesses five coins, two double-headed, two normal and one double-tailed. 
     The man shuts his eyes, picks a coin at random, and tosses the coin. He opens 
     his eyes, sees a head: what is the probability the lower face is also a head?'.

  The book gives an answer that is 2/3
  ....
  """

  The model
    - first select a coin
    - observe that the upside is a head
    - flip the coin (i.e. turn it)
  What is the probability that the flip side is also a head.

  This model give the probability that
     flip==head
  as 2/3.

  cf ~/blog/five_coins.blog

*/

// E[flip=head, coin=doubleHead,coin=normalCoin,coin=doubleTail]
// E[r₁,r₂,r₃,r₄] = (2/3,2/3,1/3,0)
// (0.666666666666667,0.666666666666667,0.333333333333333,0)
def main() {
  head := 0;
  tail := 1;
  head_tail := [head,tail];

  doubleHead := 0;
  normalCoin := 1;
  doubleTail := 2;
  coinTypes := [doubleHead,normalCoin,doubleTail];

  // Toss the coin
  coin := coinTypes[categorical([2/5,2/5,1/5])];

  toss := 0;
  if coin == doubleHead {
    toss = head;
  } else if coin == doubleTail {
    toss = tail;
  } else {
    toss = head_tail[categorical([1/2,1/2])];
  }


  // The other side (flip side)
  flip := 0;
  if coin == doubleHead {
    flip = head;
  } else if coin == doubleTail {
    flip = tail;
  } else {
    if toss == head {
      flip = tail;
    } else {
      flip = head;
    }
  }

  observe(toss = head);
  return(
          flip==head, // The question asked
          coin==doubleHead,
          coin==normalCoin,
          coin==doubleTail
          );
  
}