/+

   https://www.youtube.com/watch?v=e1Ykk_CqKTY&t=458s

   Probabilistic Programming: What It Is and How It Works - Noel Welsh

   We can see either 1, 2, or 3 cats.
   There are 3 different enticements:

   - Milkshake
   - Fish
   - Nothing

   And there are different probabilities how many cats there are given
   an enticement, see below.

   Now: We see 3 cats, what is the probability that it's a milkshake?

   The video got the following (for 3 cats):
    - milkshake: 0.42
    - fish: 0.04
    - nothing: 0.03

   Normalized to percentage (from the video): 

   0.42/(0.42 + 0.04 + 0.03) milkshake
     0.85714285714285714286
   0.04/(0.42 + 0.04 + 0.03) fish
     0.081632653061224489796
   0.03/(0.42 + 0.04 + 0.03)  nothing
     0.061224489795918367347

   This model draw the enticement from a category distribution and
   if more like the video

   See ~/cplint/cat.pl
       ~/blog/cat.blog

   observe(cats = 3):
   E[enticement=milkshake,enticement=fish,enticement=nothing,r₄,r₅,r₆,r₇] = (6/7,4/49,3/49,18/7,12/49,9/49,3)
   (0.857142857142857,0.0816326530612245,0.0612244897959184,2.57142857142857,0.244897959183673,0.183673469387755,3)
   
+/


def model(){

  milkshake := 0;
  fish      := 1;
  nothing   := 2;
  enticements := [milkshake,fish,nothing];

  enticement := enticements[categorical([0.6,0.1,0.3])];

  nums := [1,2,3];
  milkshakeCats := 0;
  if enticement = milkshake {
     milkshakeCats = nums[categorical([0.1,0.2,0.7])];
  }

  fishCats := 0;
  if enticement = fish {
    fishCats = nums[categorical([0.2, 0.4, 0.4])];    
  }
   
  nothingCats := 0;
  if enticement = nothing {
    nothingCats = nums[categorical([0.6, 0.3, 0.1])];
  } 

  // Total number of cats
  cats := milkshakeCats + fishCats + nothingCats;

  // observe(cats == 4);
  // observe(cats == 0);
  observe(cats == 3);  
  
  return (enticement==milkshake,
          enticement==fish,
          enticement==nothing,
          milkshakeCats,fishCats,nothingCats,cats);

}


def main() {
  return model();
}